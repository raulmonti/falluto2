-- ARCHIVO DE EJEMPLO PARA FALLUTO 2.0
-- CONTIENE GRAN PARTE DE LA SINTAXIS DE FALLUTO EJEMPLIFICADA

-- ESTO ES UN COMENTARIO 

OPTIONS
-- ACA ENCONTRAMOS ALGUNAS OPCIONES DE CONFIGURACION DEL SISTEMA

SYSNAME sitema_de_ejemplo
CHECK_DEADLOCK
FAULT_FAIR_DISABLE
INST_WEAK_FAIR_DISABLE

ENDOPTIONS



PROCTYPE proctype1( contextvar1, contextvar2; synchroact1, synchroact2)

	VAR
		var1: bool
		var2: -1..8
		var3: { symb1, symb2, symb3 }

	FAULT
		fault1: var1 => var1' = FALSE is TRANSIENT
		fault2: var2 > 2 & !var1 => is STOP
		fault3:	=> is STOP(synchroact1)
		fault4: var3 in {symb2, symb3, FALSE} => is BYZ(var2)

	INIT
		var1 = (TRUE | 0 > var2) & var2 in 0..5 & var3 = symb3

	TRANS
		[synchroact1]:
		[synchroact2]:
		[synchroact2]: 
		[]: contextvar1 != contextvar2     =>     var3' = symb3, var1' = !var1 | contextvar2 != 7
		[]: ! contextvar1 in 1..2
		[trans1]:
		[trans1]:
		[trans2]:

ENDPROCTYPE

-- A VERY SIMPLE PROCTYPE
PROCTYPE proctype2(;sync)

ENDPROCTYPE


-- INSTANTIATION

INSTANCE instance1 = proctype1(5, instance2.var2, s1, s2)
INSTANCE instance2 = proctype1(instance2.var2, 9, s2, s2)
INSTANCE instance3 = proctype2(s3)
INSTANCE instance4 = proctype2(s1)


-- PROPERTIES TO CHECK

LTLSPEC G TRUE

CTLSPEC AG TRUE

NORMAL_BEHAIVIOUR -> G TRUE

FINITELY_MANY_FAULTS -> G TRUE

FINITELY_MANY_FAULT(instance1.fault1) -> G TRUE
